plugins {
    id 'com.android.application'
}
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'info.cemu.Cemu'
    compileSdk 34
    ndkVersion '26.1.10909125'
    defaultConfig {
        applicationId "info.cemu.Cemu"
        minSdk 30
        targetSdk 34
        versionCode 1
        versionName "1.0"
        ndk {
//            abiFilters("x86_64", "arm64-v8a")
            abiFilters("arm64-v8a")
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    var releaseStoreFile = keystoreProperties['storeFile'];
    signingConfigs {
        if (releaseStoreFile != null) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyStorePassword']
                storeFile file(releaseStoreFile)
                storePassword keystoreProperties['keyStorePassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (releaseStoreFile != null) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('../../../CMakeLists.txt')
        }
    }
    defaultConfig {
        externalNativeBuild {
            cmake {
                arguments(
                        "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared",
                        '-DENABLE_VCPKG=ON',
                        '-DVCPKG_TARGET_ANDROID=ON',
                        '-DENABLE_SDL=OFF',
                        '-DENABLE_WXWIDGETS=OFF',
                        '-DENABLE_OPENGL=OFF',
                        '-DBUNDLE_SPEEX=ON',
                        '-DENABLE_DISCORD_RPC=OFF',
                        '-DENABLE_NSYSHID_LIBUSB=OFF',
                        '-DENABLE_WAYLAND=OFF',
                        '-DENABLE_HIDAPI=OFF',
                )
//                abiFilters("x86_64", "arm64-v8a")
                abiFilters("arm64-v8a")
            }
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation "androidx.datastore:datastore-rxjava3:1.1.0"
    implementation "androidx.datastore:datastore-preferences:1.1.0"
    implementation "androidx.datastore:datastore:1.1.0"
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}